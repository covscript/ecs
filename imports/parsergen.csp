# Covariant Script Parser Generator v1.2.6 Release
# Release Date: Fri Sep 16 10:47:53 2022
# Copyright (C) 2017-2022 Michael Lee(李登淳)
# Github:  https://github.com/mikecovlee

@require: 210503
package parsergen
import regex
struct syntax_impl
    var boot = null
    var type = null
    var data = null
end
namespace syntax_type
@begin
    constant
        token  = 1,
        term   = 2,
        ref    = 3,
        nlook  = 4,
        repeat = 5,
        opt    = 6,
        cond   = 7,
        cond_p = 8
@end
end
function make_syntax(type, data)
    var s = new syntax_impl
    s.type = type
    s.data = data
    return move(s)
end
namespace syntax
    function token(data)
        return make_syntax(syntax_type.token, data)
    end
    function term(data)
        return make_syntax(syntax_type.term, data)
    end
    function ref(name)
        return make_syntax(syntax_type.ref, name)
    end
    function nlook(...args)
        return make_syntax(syntax_type.nlook, args)
    end
    function repeat(...args)
        return make_syntax(syntax_type.repeat, args)
    end
    function optional(...args)
        return make_syntax(syntax_type.opt, args)
    end
    function cond_or(...args)
        var data = new array
        foreach it in args do data.push_back(make_syntax(syntax_type.cond_p, it))
        return make_syntax(syntax_type.cond, data)
    end
end
class grammar
    var ext = ".*"
    var lex = null
    var stx = null
end
struct token_type
    var pos = {0, 0}
    var type = null
    var data = null
end
function make_token(pos, type, data)
    var t = new token_type
    t.pos = pos
    if t.pos[0] > 0
        --t.pos[0]
    end
    t.type = type
    t.data = data
    return move(t)
end
struct lex_error
    var text = new string
    var pos = {0, 0}
end
class lexer_type
    var lexical_set = new hash_set
    var error_log = new array
    var output = new array
    var buff = new string
    var data = null
    var wpos = {0, 0}
    var pos = {0, 0, 0}
    function cursor_forward()
        ++pos[2]
        if pos[2] != data.size
            if data[pos[2]] == '\n'
                ++pos[1]
                pos[0] = 0
            else
                ++pos[0]
            end
        end
    end
    function error(str, pos)
        var err = new lex_error
        err.text = str
        err.pos = pos
        --err.pos[0]
        error_log.push_back(move(err))
    end
    function process_token()
        if lexical_set.size > 1
            if lexical_set.exist("err")
                error("Unexpected input \"" + buff + "\"", wpos)
                lexical_set = new hash_set
                return
            else
                if lexical_set.exist("ign")
                    lexical_set.erase("ign")
                end
                if lexical_set.size > 1
                    error("Ambiguous lexical \"" + buff + "\"", wpos)
                    lexical_set = new hash_set
                    return
                end
            end
        end
        var rule = null
        foreach it in lexical_set do rule = it
        if rule != "ign"
            output.push_back(make_token(wpos, rule, buff))
        end
    end
    function run(lexical, text)
        data = text
        while pos[2] != data.size
            var ch = data[pos[2]]
            if lexical_set.empty()
                var nbuff = to_string(ch)
                foreach it in lexical
                    if !it.second.match(nbuff).empty()
                        lexical_set.insert(it.first)
                    end
                end
                if !lexical_set.empty()
                    wpos = pos
                    buff = nbuff
                else
                    error("Unknown character \'" + nbuff + "\'", pos)
                end
                cursor_forward()
            else
                var nbuff = buff + ch
                var nset = new hash_set
                foreach it in lexical_set
                    if lexical[it].match(nbuff).empty()
                        nset.insert(it)
                    end
                end
                nset = hash_set.subtract(lexical_set, nset)
                if nset.empty()
                    process_token()
                    lexical_set = new hash_set
                else
                    lexical_set = nset
                    buff = nbuff
                    cursor_forward()
                end
            end
        end
        process_token()
        return output
    end
end
function print_header(txt)
    foreach i in range(txt.size) do system.out.print('#')
    system.out.println("")
    system.out.println(txt)
    foreach i in range(txt.size) do system.out.print('#')
    system.out.println("")
end
struct syntax_tree
    var root = new string
    var nodes = new array
end
struct parse_stage
    var product = new syntax_tree
    var cursor = 0
end
struct parse_error
    var cursor = 0
    var text = new string
    var pos = {0, 0}
end
namespace parse_state
@begin
    constant
        accept = 2,
        reject = 1,
        eof    = 0
@end
end
struct bootset_type
    var data_set = new hash_set
    var type_set = new hash_set
    var pending_ref = new hash_set
    function predict(token)
        return data_set.exist(token.data) || type_set.exist(token.type)
    end
    function all_empty()
        return data_set.empty() && type_set.empty() && pending_ref.empty()
    end
    function empty()
        return data_set.empty() && type_set.empty()
    end
    function merge(set)
        data_set = hash_set.merge(data_set, set.data_set)
        type_set = hash_set.merge(type_set, set.type_set)
        pending_ref = hash_set.merge(pending_ref, set.pending_ref)
    end
end
class parser_type
    var error_log = null
    var max_cursor = 0
    var max_prediction_pass = 20
    var predict_cache = null
    var ign_bootset = null
    var on_ign = false
    var stack = null
    var syn = null
    var lex = null
    var log = false
    function push_stage(root)
        var prev_cursor = 0
        if !stack.empty()
            prev_cursor = stack.front.cursor
        end
        stack.push_front(new parse_stage)
        link top = stack.front
        top.product.root = root
        top.cursor = prev_cursor
    end
    function pop_stage()
        return stack.pop_front()
    end
    function push(val)
        stack.front.product.nodes.push_back(val)
    end
    function push_token()
        link top = stack.front
        top.product.nodes.push_back(lex[top.cursor++])
    end
    function cursor()
        return stack.front.cursor
    end
    function eof()
        return stack.front.cursor >= lex.size
    end
    function peek()
        return lex.at(stack.front.cursor)
    end
    function get()
        return lex.at(stack.front.cursor++)
    end
    function error(str, pos)
        var err = new parse_error
        err.cursor = stack.front.cursor
        err.text = str
        err.pos = pos
        if err.cursor > max_cursor
            max_cursor = err.cursor
        end
        error_log.push_back(move(err))
    end
    function get_log(n)
        var set = new hash_set
        var arr = new array
        foreach it in error_log
            if it.cursor >= max_cursor - n && !set.exist(it.text)
                set.insert(it.text)
                arr.push_back(it)
            end
        end
        return move(arr)
    end
    function accept()
        link prev_stage = pop_stage()
        push(prev_stage.product)
        stack.front.cursor = prev_stage.cursor
    end
    function merge()
        link prev_stage = pop_stage()
        link top = stack.front
        foreach it in prev_stage.product.nodes do top.product.nodes.push_back(move(it))
        stack.front.cursor = prev_stage.cursor
    end
    function match_syntax(seq)
        foreach it in seq
            var result = this.match(it)
            if result != parse_state.accept
                if cursor() >= lex.size
                    error("Incomplete sentence", lex.back.pos)
                else
                    error("Incomplete sentence", peek().pos)
                end
                return result
            end
        end
        return parse_state.accept
    end
    function try_ignore()
        var cur = null
        if !on_ign && syn.exist("ignore")
            if ign_bootset != null && stack.front.cursor < lex.size && !ign_bootset.predict(lex.at(stack.front.cursor))
                return null
            end
            on_ign = true
            push_stage("ignore")
            if match_syntax(syn.ignore) == parse_state.accept
                link prev_stage = pop_stage()
                cur = prev_stage.cursor
            else
                pop_stage()
            end
            on_ign = false
        end
        return move(cur)
    end
    function ignore()
        var cur = try_ignore()
        if cur != null
            stack.front.cursor = cur
        end
    end
    function predict(set)
        if eof()
            return parse_state.eof
        end
        if set != null && !set.empty()
            link token = peek()
            if set.data_set.exist(token.data) || set.type_set.exist(token.type)
                return parse_state.accept
            end
            var cur = try_ignore()
            if cur != null && cur < lex.size
                token := lex.at(cur)
                if set.data_set.exist(token.data) || set.type_set.exist(token.type)
                    return parse_state.accept
                end
            end
            return parse_state.reject
        else
            return parse_state.accept
        end
    end
    function match(it)
        switch it.type
            case syntax_type.token
                if eof()
                    error("Early EOF, expected token \'" + it.data + "\'", lex.back.pos)
                    return parse_state.eof
                end
                if peek().type != it.data
                    ignore()
                end
                if eof()
                    return parse_state.eof
                end
                if peek().type == it.data
                    push_token()
                    return parse_state.accept
                else
                    error("Unexpected token \'" + peek().data + "\', expected <" + it.data + ">", peek().pos)
                    return parse_state.reject
                end
            end
            case syntax_type.term
                if eof()
                    error("Early EOF, expected token \'" + it.data + "\'", lex.back.pos)
                    return parse_state.eof
                end
                if peek().data != it.data
                    ignore()
                end
                if eof()
                    return parse_state.eof
                end
                if peek().data == it.data
                    push_token()
                    return parse_state.accept
                else
                    error("Unexpected token \'" + peek().data + "\', expected \'" + it.data + "\'", peek().pos)
                    return parse_state.reject
                end
            end
            case syntax_type.ref
                var result = predict(predict_cache.at(it.data))
                if result != parse_state.accept
                    return result
                end
                push_stage(it.data)
                result = match_syntax(syn.at(it.data))
                if result == parse_state.accept
                    accept()
                    return parse_state.accept
                else
                    pop_stage()
                    return result
                end
            end
            case syntax_type.nlook
                var result = predict(it.boot)
                switch result
                    case parse_state.reject
                        return parse_state.accept
                    end
                    case parse_state.eof
                        return parse_state.eof
                    end
                end
                push_stage("nlook")
                result = match_syntax(it.data)
                pop_stage()
                switch result
                    case parse_state.accept
                        return parse_state.reject
                    end
                    case parse_state.reject
                        return parse_state.accept
                    end
                    case parse_state.eof
                        return parse_state.eof
                    end
                end
            end
            case syntax_type.repeat
                loop
                    var result = predict(it.boot)
                    switch result
                        case parse_state.reject
                            return parse_state.accept
                        end
                        case parse_state.eof
                            return parse_state.accept
                        end
                    end
                    push_stage("repeat")
                    result = match_syntax(it.data)
                    switch result
                        case parse_state.accept
                            merge()
                        end
                        case parse_state.reject
                            pop_stage()
                            return parse_state.accept
                        end
                        case parse_state.eof
                            pop_stage()
                            return parse_state.accept
                        end
                    end
                end
            end
            case syntax_type.opt
                var result = predict(it.boot)
                switch result
                    case parse_state.reject
                        return parse_state.accept
                    end
                    case parse_state.eof
                        return parse_state.accept
                    end
                end
                push_stage("optional")
                result = match_syntax(it.data)
                switch result
                    case parse_state.accept
                        merge()
                        return parse_state.accept
                    end
                    case parse_state.reject
                        pop_stage()
                        return parse_state.accept
                    end
                    case parse_state.eof
                        pop_stage()
                        return parse_state.accept
                    end
                end
            end
            case syntax_type.cond
                foreach seq in it.data
                    var result = predict(seq.boot)
                    switch result
                        case parse_state.reject
                            continue
                        end
                        case parse_state.eof
                            return parse_state.eof
                        end
                    end
                    push_stage("cond_or")
                    result = match_syntax(seq.data)
                    switch result
                        case parse_state.accept
                            merge()
                            return parse_state.accept
                        end
                        case parse_state.reject
                            pop_stage()
                        end
                        case parse_state.eof
                            pop_stage()
                            return parse_state.eof
                        end
                    end
                end
                return parse_state.reject
            end
        end
        return parse_state.reject
    end
    function prep_syntax(seq)
        var set = new bootset_type
        var scanning = false
        foreach it in seq
            switch it.type
                case syntax_type.term
                    if !scanning
                        set.data_set.insert(it.data)
                        scanning = true
                    end
                end
                case syntax_type.token
                    if !scanning
                        set.type_set.insert(it.data)
                        scanning = true
                    end
                end
                case syntax_type.ref
                    if !scanning
                        set.pending_ref.insert(it.data)
                        scanning = true
                    end
                end
                case syntax_type.nlook
                    if it.boot == null
                        var ret = prep_syntax(it.data)
                        if !ret.all_empty()
                            it.boot := ret
                        end
                    end
                end
                default
                    if it.boot == null
                        var ret = prep_syntax(it.data)
                        if !ret.all_empty()
                            it.boot := ret
                        end
                    end
                    if !scanning && it.boot != null
                        set.merge(it.boot)
                    end
                end
                case syntax_type.cond
                    var scanned = scanning
                    foreach cond_p in it.data
                        if cond_p.boot == null
                            var ret = prep_syntax(cond_p.data)
                            if !ret.all_empty()
                                cond_p.boot := ret
                            end
                        end
                        if !scanning && cond_p.boot != null
                            set.merge(cond_p.boot)
                            scanned = true
                        end
                    end
                    scanning = scanned
                end
            end
        end
        return move(set)
    end
    function expand_pending_ref(boot)
        var unsolved_ref = false
        if boot != null && !boot.pending_ref.empty()
            var pending_set = new bootset_type
            var solved_ref = new hash_set
            foreach ref in boot.pending_ref
                if predict_cache.exist(ref)
                    link set = predict_cache.at(ref)
                    if !set.all_empty()
                        if set.pending_ref.empty()
                            solved_ref.insert(ref)
                            pending_set.merge(set)
                        else
                            unsolved_ref = true
                        end
                    end
                end
            end
            boot.merge(pending_set)
            boot.pending_ref = hash_set.subtract(boot.pending_ref, solved_ref)
        end
        return !unsolved_ref
    end
    function solve_pending_ref()
        var pass = 0
        loop
            ++pass
            var unsolved_ref = false
            foreach rule in syn
                if !expand_pending_ref(predict_cache.at(rule.first))
                    unsolved_ref = true
                end
                foreach it in rule.second
                    if !expand_pending_ref(it.boot)
                        unsolved_ref = true
                    end
                end
            end
        until !unsolved_ref || pass > max_prediction_pass
        if pass > max_prediction_pass
            throw runtime.exception("Reaches max pass of prediction.")
        end
    end
    function init(grammar)
        predict_cache = new hash_map
        syn = grammar
        foreach it in syn
            predict_cache.insert(it.first, prep_syntax(it.second))
        end
        if predict_cache.exist("ignore")
            ign_bootset := predict_cache.ignore
        end
        solve_pending_ref()
    end
    function parse(lex_output)
        error_log = new array
        stack = new array
        max_cursor = 0
        on_ign = false
        lex = lex_output
        push_stage("begin")
        return match_syntax(syn.begin) == parse_state.accept && stack.size == 1 && eof()
    end
    function run(grammar, lex_output)
        init(grammar)
        return parse(lex_output)
    end
    function production()
        if stack != null
            return stack.front.product
        else
            return null
        end
    end
end
function print_error(file, code, err)
    foreach it in err
        system.out.print("File \"" + file + "\", line " + (it.pos[1] + 1) + ": ")
        system.out.println(it.text)
        system.out.println("> " + code[it.pos[1]])
        foreach i in range(it.pos[0] + 2) do system.out.print(' ')
        system.out.print("^")
        system.out.println("\n")
    end
end
function print_ast_impl(indent, tree)
    if tree == null
        return
    end
    system.out.println(tree.root)
    foreach it in tree.nodes
        foreach i in range(indent + 2) do system.out.print(' ')
        system.out.print(tree.root + " -> ")
        if typeid it == typeid syntax_tree
            print_ast_impl(indent + 2, it)
        end
        if typeid it == typeid token_type
            system.out.println("\"" + it.data + "\"")
        end
    end
end
function print_ast(tree)
    print_ast_impl(0, tree)
end
class generator
    var rules = new hash_map
    var input = new string
    var code_buff = new array
    var token_buff = null
    var ast = null
    var lexer = null
    var parser = null
    var stop_on_error = true
    var enable_log = false
    var file_path = "<FILE>"
    function priv_run(lang)
        if rules.exist(lang)
            lexer = new lexer_type
            if rules[lang].lex == null
                print_header("Lexical rules not found! Stop")
                return false
            end
            token_buff = lexer.run(rules[lang].lex, input)
            if !lexer.error_log.empty()
                if stop_on_error
                    print_header("Compilation Error")
                else
                    print_header("Compilation Warning")
                end
                print_error(file_path, code_buff, lexer.error_log)
                if stop_on_error
                    return false
                end
            end
            parser = new parser_type
            parser.log = enable_log
            if rules[lang].stx == null
                print_header("Syntactic rules not found! Stop")
                return false
            end
            if parser.run(rules[lang].stx, token_buff)
                ast = parser.production()
                return true
            else
                print_header("Compilation Error")
                var err = {(lexer.error_log)..., (parser.get_log(0))...}
                err.sort([](lhs, rhs)->lhs.pos[1] < rhs.pos[1])
                print_error(file_path, code_buff, err)
                return false
            end
        else
            return false
        end
    end
    function add_grammar(lang, gram)
        rules[lang] = gram
    end
    function from_file(path)
        var ifs = iostream.ifstream(path)
        if !ifs.good()
            return false
        end
        file_path = path
        input = new string
        code_buff = new array
        while ifs.good()
            var line = ifs.getline()
            input += line + "\n"
            for i = 0, i < line.size, ++i
                if line[i] == '\t'
                    line.assign(i, ' ')
                end
            end
            code_buff.push_back(line)
        end
        foreach it in rules
            var reg = regex.build(it.second.ext)
            if !reg.match(path).empty()
                return priv_run(it.first)
            end
        end
        return false
    end
    function from_string(lang, str)
        input = str
        code_buff = input.split({'\n'})
        return priv_run(lang)
    end
end
